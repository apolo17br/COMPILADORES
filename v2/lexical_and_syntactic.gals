#Options
GenerateScanner = true
GenerateParser = true
Language = Java
ScannerName = Lexico
ParserName = Sintatico
SemanticName = Semantico
Package = gals
ScannerCaseSensitive = true
ScannerTable = Compact
Input = String
Parser = SLR
#RegularDefinitions
ignore: [\t\r\s]+|(#.*\n)|[#][!][#-~\n\r\s\t\b]+[!][#]*
identifier: [a-zA-Z_][a-zA-Z0-9_]*

// TYPES
int: [0-9]+
float: [0-9]*\.[0-9]+
boolean: true | TRUE | false | FALSE
binary: 0b[0-1]+
hexadecimal: 0x[0-9a-fA-F]+
string: \"[^\"]*\"
char: '[^']'
#Tokens
// OTHERS
: {ignore}
NEW_LINE: \n

// OTHERS RESERVED WORDS / CHARACTERS
BREAK: "break" 
COMMA: ","
STRUCTURE_REFERENCE: "."
COLON: ":"
RETURN: "return"
IN: "in"
NULL: "null"
FUNCTION_RETURN: "->"
SCOPE_OPEN: "{"
SCOPE_CLOSE: "}"
SUBGROUP_OPEN: "("
SUBGROUP_CLOSE: ")"
INDEX_OPEN: "["
INDEX_CLOSE: "]"

// RESERVED FUNCTIONS
MAIN_FUNCTION: "@main"
PRINT: "@print"
INPUT: "@input"
RANGE: "@range"

// IDENTIFIERS
VAR: "$"{identifier}
CONST: "&"{identifier}
FUNCTION: "@"{identifier}

// CONDITIONAL
IF: "if"
ELSE: "else"
ELSIF: "elsif"
SWITCH: "switch"
CASE: "case"
DEFAULT: "default"

// LOOP
WHILE: "while"
DO: "do"
FOR: "for"
IN: "in"
CONTINUE: "continue"

// TYPES
TYPE_VOID: "void"
TYPE_INT: "int"
TYPE_FLOAT: "float"
TYPE_DOUBLE: "double"
TYPE_STRING: "string"
TYPE_CHAR: "char"
TYPE_BOOLEAN: "boolean"

// TYPES VALUES
INT: {int}
FLOAT: {float}
BOOLEAN: {boolean}
BINARY: {binary}
HEXADECIMAL: {hexadecimal}
STRING: {string}
CHAR: {char}

// ARITHMETIC OPERATORS
OP_ASSIGN: "="
OP_ADD: "+"
OP_SUB: "-"
OP_MULT: "*"
OP_DIV: "/"
OP_MOD: "%"
OP_INC: "++"
OP_DEC: "--"

// RELATIONAL OPERATORS
OP_LT: "<"
OP_LTE: "<="
OP_EQ: "=="
OP_NEQ: "!="
OP_GT: ">"
OP_GTE: ">="

// LOGICAL OPERATORS
OP_AND: "&&"
OP_OR: "||"
OP_NOT: "!"

// BITWISE OPERATORS
OP_BW_NOT: "~"
OP_BW_AND: "&"
OP_BW_OR: "|"
OP_BW_XOR: "^"
OP_BW_LS: "<<"
OP_BW_RS: ">>"
#NonTerminals
// MAIN STRUCTURE ---
<program>
<main_function>
<function_list>
<function>
// ------------------


// FUNCTION PARAMETERS
//
<fn_parameter_list>
//
<fn_parameters>
//
<fn_parameter>
//
// -------------------


// PARAMETER ----
//
<parameters>
//
// -------------------


// PARAMETER VALUES
<param_values>
<param_value>
// -------------------


// VALUES ------------
//
<values>
//
// -------------------


// ATTRIBUTES --------
//
<attributes>
//
<attribute>
//
// -------------------


// TYPES -------------
//
<type>
//
<primitive_type>
//
//
<type_value>
//
//--------------------


// ARRAYS ------------
<array>
//
<array_values>
<array_index>
<array_size>
//
// -------------------


// MISC --------------
<new_line>
<scope>
// -------------------


// COMMANDS ----------
<command_list>
<command>
<function_call>
// -------------------


// CONDITIONAL -------
<if>
<else>
<elsif>
<single_elsif>
// -------------------


// LOOPS -------------
<while>
<do_while>
<for>
<range>
<range_types>
// -------------------


// EXPRESSIONS -------
<exp>
<exp1>
<exp2>
<exp3>
<exp4>
<exp5>
<exp6>
<exp7>
<exp8>
<exp9>
<exp10>
<exp11>
// -------------------


// OPERATORS ---------
<op_relational>
<op_negate>
<op_bitwise>
<op_arithmetic_low>
<op_arithmetic_high>
// -------------------
#Grammar
// MAIN STRUCTURE ----------
<program> ::= <main_function> | <new_line> <main_function> | <function_list> <main_function> | <new_line> <function_list> <main_function>;
<main_function> ::= MAIN_FUNCTION #10 <fn_parameter_list> FUNCTION_RETURN <type> #11 <scope> <new_line> | MAIN_FUNCTION #10 <fn_parameter_list> FUNCTION_RETURN <type> #11 <scope>; 
<function_list> ::= <function> | <function> <function_list>;
<function> ::= FUNCTION #10 <fn_parameter_list> FUNCTION_RETURN <type> #11 <scope> #12 <new_line>;
// -------------------------


// FUNCTION PARAMETERS ----------
// Used in function declaration
<fn_parameter_list> ::= SUBGROUP_OPEN <fn_parameters> SUBGROUP_CLOSE | SUBGROUP_OPEN SUBGROUP_CLOSE;
// Ex.: ($a: int) | ()
<fn_parameters> ::= <fn_parameter> #21 | <fn_parameter> #21 COMMA <fn_parameters>;
// Ex.: $a: int | $a: int, $b: string, &c: float
<fn_parameter> ::= VAR #20 COLON <type> | CONST #20 COLON <type>;
// Ex.: $a: int | &b: string
// ------------------------------


// PARAMETERS ----------
// Used in function call
<parameters> ::= SUBGROUP_OPEN <param_values> SUBGROUP_CLOSE | SUBGROUP_OPEN SUBGROUP_CLOSE;
// Ex.: (1, 5, "Hello", 1 + 2, TRUE)
// --------------------------


// PARAMETER VALUES
<param_values> ::= <param_value> | <param_value> COMMA <param_values>;
<param_value> ::= INT #211 | VAR #212;
// -----------------


// VALUES -------------------
// Values separated by comma
<values> ::= <exp> | <exp> COMMA <values>;
// Ex.: 1, 5, "Hello", 1 + 2, TRUE
// --------------------------


// ATTRIBUTES ----------
// Attributes (assignable)
<attributes> ::= <attribute> | <attribute> COMMA <attributes>;
// Ex.: $a | $a, &PI, $array[1]
<attribute> ::= VAR #810 | CONST #811 | <array>;
// Ex.: $a | &PI | $a[2] | &TEST[1]
// ---------------------


// TYPES ----------
// Primitive type or primitive type as array
<type> ::= <primitive_type> #100 | <primitive_type> #101 <array_size>;
// Primitive type
<primitive_type> ::= TYPE_VOID | TYPE_INT | TYPE_FLOAT | TYPE_DOUBLE | TYPE_STRING | TYPE_CHAR | TYPE_BOOLEAN;
// Ex.: void | int | float | double | string | char | boolean
// Single value
<type_value> ::= INT #800 | FLOAT #801 | BOOLEAN #802 | BINARY | HEXADECIMAL | STRING #803 | CHAR #804;
// Ex.: 1 | 5 | "Hello" | TRUE
// ----------------


// ARRAYS ----------
<array> ::= VAR #812 <array_index> | CONST #813 <array_index>;
// Ex.: $a[5] | &b[1 + 2]
<array_values> ::= INDEX_OPEN <values> INDEX_CLOSE;
<array_index> ::= INDEX_OPEN <exp> INDEX_CLOSE;
<array_size> ::= INDEX_OPEN INT #102 INDEX_CLOSE | INDEX_OPEN INDEX_CLOSE;
// Ex.: $a[2] | $a[] (sizeless)
// -----------------


// MISC ----------
<new_line> ::= NEW_LINE | NEW_LINE <new_line>;
<scope> ::= SCOPE_OPEN #900 <new_line> <command_list> SCOPE_CLOSE #901 | SCOPE_OPEN #900 <new_line> SCOPE_CLOSE #901;
// ---------------


// COMMANDS ----------
<command_list> ::= <command> <new_line> | <command> <new_line> <command_list>;
// ASSIGNMENTS
<command> ::= <attributes> | <attributes> OP_ASSIGN <values>;
<command> ::= <attribute> OP_ASSIGN #850 <exp> #851;
// INPUT / OUTPUT
<command> ::= PRINT SUBGROUP_OPEN <attribute> #201 SUBGROUP_CLOSE | PRINT SUBGROUP_OPEN INT #202 SUBGROUP_CLOSE;
<command> ::= <attribute> OP_ASSIGN INPUT SUBGROUP_OPEN SUBGROUP_CLOSE #200;
// FUNCTION CALL
<command> ::= <function_call> | <attribute> OP_ASSIGN <function_call> #216;
// Ex.:
<function_call> ::= FUNCTION #210 <parameters> #213;
// Ex.: @calculate(1, '+', 2)
// CONDITIONAL
<command> ::= <if> #302 | <if> <else>;
<command> ::= <if> <elsif>;
<command> ::= <if> <elsif> <else>;
// LOOP
<command> ::= <while>;
<command> ::= <do_while>;
<command> ::= <for>;
// SPECIAL
<command> ::= RETURN INT #214 | RETURN VAR #215;
// ------------------


// CONDITIONAL ----------
<if> ::= IF #300 SUBGROUP_OPEN <exp> SUBGROUP_CLOSE #301 <scope>;
<else> ::= ELSE #303 <scope> #304;
<elsif> ::= <single_elsif> | <single_elsif> <elsif>;
<single_elsif> ::= ELSIF SUBGROUP_OPEN <exp> SUBGROUP_CLOSE <scope>;
// ----------------------


// LOOPS ----------
<while> ::= WHILE #310 SUBGROUP_OPEN <exp> SUBGROUP_CLOSE #311 <scope> #312;
<do_while> ::= DO #313 <scope> WHILE SUBGROUP_OPEN <exp> SUBGROUP_CLOSE #314;
<for>  ::=  FOR SUBGROUP_OPEN <range> SUBGROUP_CLOSE #324 <scope> #325;
<range> ::= <attribute> #320 IN RANGE SUBGROUP_OPEN <range_types> SUBGROUP_CLOSE;
<range_types> ::= INT | INT COMMA INT | INT #321 COMMA INT #322 COMMA INT #323;
// ----------------


// EXPRESSIONS ----------
<exp>  ::= <exp> OP_OR #824 <exp1> | <exp1>;
<exp1> ::= <exp1> OP_AND #824 <exp2> | <exp2>;
<exp2> ::= <exp2> OP_BW_OR <exp3> | <exp3>;
<exp3> ::= <exp3> OP_BW_XOR <exp4> | <exp4>;
<exp4> ::= <exp4> OP_BW_AND <exp5> | <exp5>;
<exp5> ::= <exp5> <op_relational> #825 <exp6> #826 | <exp6>;
<exp6> ::= <exp6> <op_bitwise> <exp7> | <exp7>;
<exp7> ::= <exp7> <op_arithmetic_low> <exp8> | <exp8>;
<exp8> ::= <exp8> <op_arithmetic_high> <exp9> | <exp9>;
<exp9> ::= <op_negate> <exp10> | <exp10>;
<exp10> ::= <type_value> | <attribute> | <array_values> | <function_call> | <exp11>;
<exp11> ::= SUBGROUP_OPEN <exp> SUBGROUP_CLOSE;
// ----------------------


// OPERATORS ----------
<op_relational> ::= OP_GT | OP_LT | OP_GTE | OP_LTE | OP_EQ | OP_NEQ; 
<op_negate> ::= OP_SUB | OP_BW_NOT | OP_NOT;
<op_bitwise> ::= OP_BW_LS | OP_BW_RS;
<op_arithmetic_low> ::= OP_ADD #820 | OP_SUB #821;
<op_arithmetic_high> ::= OP_MULT #822 | OP_DIV #823 | OP_MOD;
// --------------------
